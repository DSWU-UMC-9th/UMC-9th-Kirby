plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories { mavenCentral() }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // ğŸ”¹ QueryDSL : OpenFeign 7.0 (ì›Œí¬ë¶ ê·¸ëŒ€ë¡œ)
    implementation "io.github.openfeign.querydsl:querydsl-jpa:7.0"
    implementation "io.github.openfeign.querydsl:querydsl-core:7.0"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:7.0:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
}

tasks.named('test') {
    useJUnitPlatform()
}

//  QueryDSL ê´€ë ¨ ì„¤ì •
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

// ìƒì„±ëœ Q í´ë˜ìŠ¤ ê²½ë¡œë¥¼ ì†ŒìŠ¤ì…‹ì— ì¶”ê°€
sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

// ì»´íŒŒì¼ ì‹œ ìƒì„± ê²½ë¡œ ì§€ì •
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(querydslDir)
}

// clean ì‹œ ìƒì„± í´ë” ì‚­ì œ
clean.doLast {
    file(querydslDir).deleteDir()
}
